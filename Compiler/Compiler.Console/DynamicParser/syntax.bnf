<START> ::= <USINGS> <CLASSES> | <CLASSES>

<USINGS> ::= <USING> {<USING>}
<USING> ::= *<KW_USING> *<STRING> *<SEMICOLON>

<CLASSES> ::= <CLASS_DECL> {<CLASS_DECL>}
<CLASS_DECL> ::= *<KW_CLASS> *<IDENT> <CLASS_BODY> | *<KW_CLASS> *<IDENT> <EXTENDS> <CLASS_BODY>  
<EXTENDS> ::= *<KW_EXTENDS> *<IDENT>
<CLASS_BODY> ::= *<BRACE_O> <CLASS_MEMBERS> *<BRACE_C> | *<BRACE_O> *<BRACE_C>

<CLASS_MEMBERS> ::= <CLASS_MEMBER> {<CLASS_MEMBER>}
<CLASS_MEMBER> ::= <METHOD_DECL> | <CTOR_DECL> | <VARIABLE_DECL_STMT> *<SEMICOLON>
<CTOR_DECL> ::= <CTOR> <BODY>
<CTOR> ::= <CTOR_SIGNATURE> | <CTOR_SIGNATURE>  <BASE_CTOR_CALL>
<CTOR_SIGNATURE> ::= *<VISIBILITY> *<KW_CTOR> <PARAM_LIST>
<BASE_CTOR_CALL> ::= *<COLON> *<BASE> <ARG_LIST>

<METHOD_DECL> ::= *<VISIBILITY> <METHOD_SIGNATURE> <BODY> | *<VISIBILITY> <VIRTUAL_OVERRIDE> <METHOD_SIGNATURE> <BODY>
<METHOD_SIGNATURE> ::= <RETURN_TYPE> *<IDENT> <PARAM_LIST>
<BODY> ::= *<BRACE_O> <STMTS> *<BRACE_C> | *<BRACE_O> *<BRACE_C>
<VISIBILITY> ::= *<KW_PRIVATE> | *<KW_PROTECTED>| *<KW_PUBLIC>
<VIRTUAL_OVERRIDE> ::= *<KW_VIRTUAL> | *<KW_OVERRIDE>

<RETURN_TYPE> ::= <TYPE> | *<KW_VOID>
<TYPE> ::= <SYSTEM_TYPE> | *<IDENT>
<SYSTEM_TYPE> ::= *<KW_INT> | *<KW_STRING> | *<KW_FLOAT> | *<KW_BOOL>

<PARAM_LIST> ::= *<PAREN_O> <PARAMS> *<PAREN_C> | *<PAREN_O> *<PAREN_C>
<PARAMS> ::= <PARAM> {*<COMMA> <PARAM>}
<PARAM> ::= <TYPE> *<IDENT>

<STMTS> ::= <STMT> {<STMT>}
<STMT> ::= <BREAK_STMT> *<SEMICOLON> | <RETURN_STMT> *<SEMICOLON> | <CONTINUE_STMT> *<SEMICOLON> | <ASSIGN_STMT> *<SEMICOLON> | <LOCAL_VARIABLE_DECL_STMT> *<SEMICOLON> | <VARIABLE_DECL_STMT> *<SEMICOLON> | <EXPR_STMT> *<SEMICOLON> | <WRITE_STMT> *<SEMICOLON> | <READ_STMT> *<SEMICOLON> | <WHILE_STMT> | <IF_STMT>
<EXPR_STMT> ::= <EXPR>
<RETURN_STMT> ::= *<KW_RETURN> | *<KW_RETURN> <EXPR>
<BREAK_STMT> ::= *<KW_BREAK>
<CONTINUE_STMT> ::= *<KW_CONTINUE>
<ASSIGN_STMT> ::= *<IDENT> *<OP_ASSIGN> <EXPR>
<LOCAL_VARIABLE_DECL_STMT> ::= <TYPE> *<IDENT> | <TYPE> *<IDENT> *<OP_ASSIGN> <EXPR>
<VARIABLE_DECL_STMT> ::= <VISIBILITY> <TYPE> *<IDENT> | <VISIBILITY> <TYPE> *<IDENT> *<OP_ASSIGN> <EXPR>
<WHILE_STMT> ::= *<KW_WHILE> *<PAREN_O> <EXPR> *<PAREN_C> <BODY>
<IF_STMT> ::= <IF> | <IF> <ELSE>
<IF> ::= *<KW_IF> *<PAREN_O> <EXPR> *<PAREN_C> <BODY>
<ELSE> ::= *<KW_ELSE> <BODY> | *<KW_ELSE> <IF_STMT>

<WRITE_STMT> ::= *<KW_WRITE> <ARGS>
<READ_STMT> ::= *<KW_READ> <VAR_LIST>
<VAR_LIST> ::= <VAR> {<COMMA> <VAR>}

<EXPR> ::= <OR>
<OR> ::= <AND> {*<OR_OP> <AND>}
<AND> ::= <EQUALS> {*<AND_OP> <EQUALS>}
<EQUALS> ::= <COMPARE> {*<EQUALS_OP> <COMPARE>}
<COMPARE> ::= <ADD> {*<COMPARE_OP> <ADD>} 
<ADD> ::= <MULT> {*<ADD_OP> <MULT>}
<MULT> ::= <DOT> {*<MULT_OP> <DOT>} 
<DOT> ::=  <PRIMARY> {*<DOT_OP> *<IDENT>}
<PRIMARY> ::= <METHOD_CALL> | <VAR> | <NUMBER> | *<BOOL> | *<STRING> | <PAREN> | <NEG> | <NOT> | <OBJ_CREATION> | <CAST> | *<NULL> | *<THIS> | *<BASE>

<PAREN> ::= *<PAREN_O> <EXPR> *<PAREN_C>
<NEG> ::= *<ADD_OP> <EXPR>
<NOT> ::= *<OP_NOT> <EXPR>
<CAST> ::= *<PAREN_O> <TYPE> *<PAREN_C> <EXPR>
<OBJ_CREATION> ::= *<KW_NEW> *<IDENT> <ARG_LIST>
<METHOD_CALL> ::= <METHOD_NAME> <ARG_LIST>
<ARG_LIST> ::= *<PAREN_O> <ARGS> *<PAREN_C> | *<PAREN_O> *<PAREN_C>
<ARGS> ::= <EXPR> {*<COMMA> <EXPR>}
<METHOD_NAME> ::= *<IDENT>
<VAR> ::= *<IDENT>
<NUMBER> ::= *<FLOAT> | *<INT> 