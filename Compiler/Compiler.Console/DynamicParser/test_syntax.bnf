<PROGRAM> ::= [Program]@0 1 <CLASS_DECL> {<CLASS_DECL>}

<CLASS_DECL> ::= [Class]@1 @ @2 *<KW_CLASS> *<IDENT> <CLASS_BODY> | [Class]@1 @2 @3 *<KW_CLASS> *<IDENT> <EXTENDS> <CLASS_BODY>  
<EXTENDS> ::= *<KW_EXTENDS> *<IDENT>
<CLASS_BODY> ::= [ClassBody]@1 *<BRACE_O> { <CLASS_MEMBER> } *<BRACE_C>

<CLASS_MEMBER> ::= [ClassMember]@0 <METHOD_DECL> | [ClassMember]@0 <CTOR_DECL> | [ClassMember]@0 <VARIABLE_DECL_STMT> *<SEMICOLON>
<CTOR_DECL> ::= <CTOR> <BODY>
<CTOR> ::= <CTOR_SIGNATURE> <BASE_CTOR_CALL> | <CTOR_SIGNATURE>
<CTOR_SIGNATURE> ::= <VISIBILITY> *<KW_CTOR> <PARAM_LIST>
<BASE_CTOR_CALL> ::= *<COLON> *<BASE> <ARG_LIST>

<METHOD_DECL> ::= [Method]@0 @ @1 @2 @3 @4 <VISIBILITY> <RETURN_TYPE> *<IDENT> <PARAM_LIST> <BODY> | [Method]@0 @1 @2 @3 @4 @5 <VISIBILITY> <VIRTUAL_OVERRIDE> <RETURN_TYPE> *<IDENT> <PARAM_LIST> <BODY>
<BODY> ::= [MethodBody]@1 *<BRACE_O> <STMTS> *<BRACE_C> | [MethodBody]@ *<BRACE_O> *<BRACE_C>
<VISIBILITY> ::= *<KW_PRIVATE> | *<KW_PROTECTED>| *<KW_PUBLIC>
<VIRTUAL_OVERRIDE> ::= *<KW_VIRTUAL> | *<KW_OVERRIDE>

<RETURN_TYPE> ::= <TYPE> | *<KW_VOID>
<TYPE> ::= <SYSTEM_TYPE> | *<IDENT>
<SYSTEM_TYPE> ::= *<KW_INT> | *<KW_STRING> | *<KW_FLOAT> | *<KW_BOOL>

<PARAM_LIST> ::= [ParamList]@1 *<PAREN_O> <PARAMS> *<PAREN_C> | [ParamList]@ *<PAREN_O> *<PAREN_C>
<PARAMS> ::= <PARAM> {*<COMMA> <PARAM>}
<PARAM> ::= <TYPE> *<IDENT>

<STMTS> ::= <STMT> {<STMT>}
<STMT> ::= <BREAK_STMT> *<SEMICOLON> | <RETURN_STMT> *<SEMICOLON> | <CONTINUE_STMT> *<SEMICOLON> | <ASSIGN_STMT> *<SEMICOLON> | <LOCAL_VARIABLE_DECL_STMT> *<SEMICOLON> | <VARIABLE_DECL_STMT> *<SEMICOLON> | <EXPR_STMT> *<SEMICOLON> | <WRITE_STMT> *<SEMICOLON> | <READ_STMT> *<SEMICOLON> | <WHILE_STMT> | <IF_STMT>
<EXPR_STMT> ::= <EXPR>
<RETURN_STMT> ::= *<KW_RETURN> <EXPR> | *<KW_RETURN>
<BREAK_STMT> ::= *<KW_BREAK>
<CONTINUE_STMT> ::= *<KW_CONTINUE>
<ASSIGN_STMT> ::= *<IDENT> *<OP_ASSIGN> <EXPR> | *<THIS> *<DOT_OP> *<IDENT> *<OP_ASSIGN> <EXPR>
<LOCAL_VARIABLE_DECL_STMT> ::= <TYPE> *<IDENT> *<OP_ASSIGN> <EXPR> | <TYPE> *<IDENT>
<VARIABLE_DECL_STMT> ::= <VISIBILITY> <TYPE> *<IDENT> *<OP_ASSIGN> <EXPR> | <VISIBILITY> <TYPE> *<IDENT>
<WHILE_STMT> ::= *<KW_WHILE> *<PAREN_O> <EXPR> *<PAREN_C> <BODY>
<IF_STMT> ::= <IF> <ELSE> | <IF>
<IF> ::= *<KW_IF> *<PAREN_O> <EXPR> *<PAREN_C> <BODY>
<ELSE> ::= *<KW_ELSE> <IF_STMT> | *<KW_ELSE> <BODY>

<WRITE_STMT> ::= *<KW_WRITE> <ARGS>
<READ_STMT> ::= *<KW_READ> <VAR_LIST>
<VAR_LIST> ::= <VAR> {*<COMMA> <VAR>}

<EXPR> ::= <OR>
<OR> ::= <AND> *<OR_OP> <OR> | <AND>
<AND> ::= <EQUALS> *<AND_OP> <AND> | <EQUALS>
<EQUALS> ::= <COMPARE> *<EQUALS_OP> <EQUALS> | <COMPARE>
<COMPARE> ::= <ADD> *<COMPARE_OP> <COMPARE> | <ADD>
<ADD> ::= <MULT> *<ADD_OP> <ADD> | <MULT>
<MULT> ::= <DOT> *<MULT_OP> <MULT> | <DOT>
<DOT> ::=  <PRIMARY> *<DOT_OP> <DOT> | <PRIMARY>
<PRIMARY> ::= <METHOD_CALL> | <VAR> | <NUMBER> | *<BOOL> | *<STRING> | <PAREN> | <NEG> | <NOT> | <OBJ_CREATION> | <CAST> | *<NULL> | *<THIS> | *<BASE>

<MEMBER_ACCESS> ::= <METHOD_CALL> | *<IDENT>
<PAREN> ::= *<PAREN_O> <EXPR> *<PAREN_C>
<NEG> ::= *<ADD_OP> <EXPR>
<NOT> ::= *<OP_NOT> <EXPR>
<CAST> ::= *<PAREN_O> <TYPE> *<PAREN_C> <EXPR>
<OBJ_CREATION> ::= *<KW_NEW> *<IDENT> <ARG_LIST>
<METHOD_CALL> ::= <METHOD_NAME> <ARG_LIST>
<ARG_LIST> ::= *<PAREN_O> <ARGS> *<PAREN_C> | *<PAREN_O> *<PAREN_C>
<ARGS> ::= <EXPR> {*<COMMA> <EXPR>}
<METHOD_NAME> ::= *<IDENT>
<VAR> ::= *<IDENT>
<NUMBER> ::= *<FLOAT> | *<INT>